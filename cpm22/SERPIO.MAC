;
; PARALLEL PROCEDURES FOR CP/M
;
;		HB	10061982
;
PRTCNT	EQU	0CH	;PIO_A_DATA PORT ==> PRINTER CONTROL PORT
PRTDAT	EQU	0DH	;PIO_B_DATA PORT ==> PRINTER DATA PORT
PARALL	EQU	0
SERIEA	EQU	2
SERIEB	EQU	1
 
;
BUSY	EQU	4	; BUSY LINE BIT

	.Z80

	GLOBAL	PINIT	; PARALLEL INITIALISATION
	GLOBAL	LIST	; CHARACTER TO PRINTER
	GLOBAL	LISTST	; PRINTER STATUS
 
	EXTERNAL	CONIN
	EXTERNAL	CONOUT
	EXTERNAL	CONST
	EXTERNAL	COBOUT
	EXTERNAL	LSTFLG


;
PINIT:
 
	LD	A,0FH		; PIO_B OUTPUT MODE
	OUT	(PRTDAT+2),A
 
	LD	A,07H		; NO INTERRUPT
	OUT	(PRTDAT+2),A	; PORT B CONTROL PORT
 
	OUT	(PRTCNT+2),A	; PORT A CONTROL, NO INTERRUPT
 
	LD	A,0CFH
	OUT	(PRTCNT+2),A	; BIDIRECTIONEEL
 
	LD	A,3CH		; SET DIRECTION OF LINES
	OUT	(PRTCNT+2),A	; PORT A CONTROL PORT
 
	LD	A,1		; SET PRIME LINE
	OUT	(PRTCNT),A
 
	LD	A,11H		; SELECT
	OUT	(PRTDAT),A	;SEND IT
 
	CALL	PRTSTR		; STOBE OUTPUT
 
	RET
 
;
; PRINT CHARACTER NAAR PRINTER
;
; CHARACTER IN C
;
 
LIST:
	LD	A,(LSTFLG)
	CP	SERIEB

	JR	Z,LIST1
	CALL	PRTWYT		; WAIT
	LD	A,C
	AND	7FH
	OUT	(PRTDAT),A
	CALL	PRTSTR		; STOBE
	RET
LIST1:
	CALL	COBOUT
	RET
 
 
;
; STROBE
;
PRTSTR:
	PUSH	AF
	LD	A,2
	OUT	(PRTCNT),A
	LD	A,3
	OUT	(PRTCNT),A
	POP	AF
	RET
 
;
; WAIT
;
PRTWYT:
	IN	A,(PRTCNT)
	BIT	BUSY,A
	JP	NZ,PRTWYT
	RET
;
;	PRINTER STATUS
LISTST:
	LD	A,0		; NEVER READY
	RET


	END
