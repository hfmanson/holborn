;
SERCON	EQU	022H	; DART A-SIDE CONTROL PORT
SERDAT	EQU	020H	; DART A-SIDE DATA PORT
SERCOB	EQU	23H
SERDAB	EQU	21H
 
TXRDY	EQU	2	; TX RDY BIT
RXRDY	EQU	0	; RX RDY BIT

MASK	EQU	28H	;MASK FOR STATUS OF RR0
TEST	EQU	28H	;TEST FOR CTS AND DCD STATUS INPUT
RS232	EQU	0	;RS232 PROTOCOL
XONOFF	EQU	1	;RS232_C PROTOCOL
HANDSK	EQU	2	;HANDSHAKE PROTOCOL (DCD/CTS)
XON	EQU	11H	;X_ON
XOFF	EQU	13H	;XOFF
STATRS	EQU	10H	;RESET EXTERNAL STATUS

	.Z80

	GLOBAL	SINITA	; SERIAL INITIALISATION OF DART A AND B
	GLOBAL	SINITB	; SERIAL INITIALISATION OF DART B ONLY
	GLOBAL	CONIN	; CONSOLE IN
	GLOBAL	CONOUT	; CONSOLE OUT
	GLOBAL	CONST	; CONSOLE STATUS
	GLOBAL	COBIN
	GLOBAL	COBOUT
	GLOBAL	COBST
	GLOBAL	READER	;CONSOLE B INPUT
	GLOBAL	PUNCH	;CONSOLE B OUTPUT

	EXTERNAL	PROTOC
	EXTERNAL	DIVIDE
	EXTERNAL	HNDMSK
;
;INITIALIZE DART - A
;
 
SINITA:
	LD	C,SERCON
	LD	HL,INIOUT
	LD	B,LINIOU
 
	OTIR
	LD	DE,0
	ADD	HL,DE		;OFFSET FOR CLOCKMODE
	OUTI
 
SINITB:
	LD	C,SERCOB
	LD	HL,INIOUT
	LD	B,LINIOU
 
	OTIR		; INITALIZE PORT B

	LD	DE,(DIVIDE)
	LD	D,0
	ADD	HL,DE		;OFFSET FOR CLOCKMODE
	OUTI
	RET
 
CONIN:	IN	A,(SERCON)	; WAIT FOR FLAG
	BIT	RXRDY,A
	JR	Z,CONIN		; NOT READY YET
	IN	A,(SERDAT)
	AND	07FH		; CLEAR PARITY
	RET
 
 
 
CONOUT:
TTYBSY:	IN	A,(SERCON)	; CHECK FOR BUSY STATUS
	BIT	TXRDY,A
	JR	Z,TTYBSY
	LD	A,C		; CHARACTER TO ACC.
	OUT	(SERDAT),A
	RET
 
CONST:				; CHECK DART INPUT
	IN	A,(SERCON)
	BIT	RXRDY,A
	LD	A,0
	RET	Z		; NO INPUT
	LD	A,0FFH		; SET STATUS TRUE
	RET
PAGE
 
READER:
COBIN:
	IN	A,(SERCOB)
	BIT	RXRDY,A
	JR	Z,COBIN
	CALL	INTSTB
	RET	NC		;VALID CHACACTER LOAD IN A
	JR	COBIN		;WAIT FOR X_ON AND NEXT VALID CHAR.
INTSTB:
	IN	A,(SERDAB)
	AND	07FH
	CP	XON
	JR	Z,COBI10
	CP	XOFF
	JP	Z,COBI10
	AND	A		;CLEAR CARRY
	RET
COBI10:
	PUSH	AF		;SAVE INPUT CHARACTER
	LD	A,(PROTOC)
	CP	XONOFF		;RS232_C PROTOCOL SELECT ?
	JR	Z,COBI20	;YES
	POP	AF		;NO,RESTORE INPUT CHARACTER
	AND	A		;CLEAR CARRY
	RET
;
; RS232_C PROTOCOL AND X_ON OR X_OFF RECEIVED
;
COBI20:
	POP	AF		;RETRIEVE INPUT CHARACTER
	CP	XON
	JR	NZ,COBI25
	XOR	A		;X_ON RECEIVED
COBI25:
	LD	(XFLAG),A	;XFLAG:=0    - X_ON RECEIVED
				;XFLAG:=XOFF - X_OFF RECEIVED
	SCF			;SET CARRY
	RET

PUNCH:
COBOUT:
	IN	A,(SERCOB)
	BIT	TXRDY,A
	JR	Z,COBOUT
	LD	A,(PROTOC)
	CP	RS232
	JR	Z,OUTPB		;RS232 PROTOCOL
	CP	XONOFF
	JR	Z,COBO20	;RS232_C PROTOCOL (X_ON/X_OFF)
;
;RS232 HANDSHAKING PROTOCOL
;
COBO10:
	LD	A,(HNDMSK)
	LD	B,A		;HANDSHAKE MASK (CTS=20H,DCD=08H)
	LD	A,STATRS
	OUT	(SERCOB),A	;RESET EXTERNAL STATUS LATCH
	IN	A,(SERCOB)	;READ STATUS FROM RR0
	AND	B
	CP	B
	JR	NZ,COBO10	;LOOP UNTIL HANDSHAKE OKE
	JR	OUTPB		;OUTPUT CHARACTER
;
;RS232_C PROTOCOL (X_ON/X_OFF)
;
COBO20:
	CALL	COBST		;CHECK FOR INPUT
	AND	A
	JR	Z,COBO30	;NO INPUT
COBO25:
	CALL	INTSTB		;GET CHARACTER
COBO30:
	LD	A,(XFLAG)
	AND	A		;X_ON ?
	JR	Z,OUTPB		;YES,OUTPUT CHARACTER
	JR	COBO25		;NO,IS XOFF LOOP UNTIL X_ON RECEIVED
;
;OUTPUT CHARACTER IN C REGISTER TO DART-B
OUTPB:
	LD	A,C
	OUT	(SERDAB),A
	RET
COBST:
	IN	A,(SERCOB)
	BIT	RXRDY,A
	LD	A,0
	RET	Z
	LD	A,0FFH
	RET

INIOUT:
	DEFB	18H	;ERROR RESET
	DEFB	03H
	DEFB	0C1H	;RX_8 BITS-CHAR,RECEIVE ENABLE
	DEFB	05H
	DEFB	0EAH	;2 STOP BITS,TX_8 BITS-CHAR,DTR,RTS,
			;TRANSMIT ENABLE
	DEFB	04H
LINIOU	EQU	$-INIOUT
WR4_1:	DEFB	4CH	;1/16 CLOCKMODE , 2 STOP BITS , NO PARITY
WR4_2:	DEFB	8CH	;1/32 CLOCKMODE , 2 STOP BITS , NO PARITY
WR4_4:	DEFB	0CCH	;1/64 CLOCKMODE , 2 STOP BITS , NO PARITY


XFLAG:	DEFB	0	; X_ON / X_OFF FLAG (INITIAL SET TO X_ON)



	END

