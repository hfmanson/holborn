;
; HOLBORN COMMEN PART FOR FLOPPY AND HARD DISK IO
;
	.Z80
;			HB	10061982
;			HB	#1	24081982
;
;
; GLOBAL PROCEDURES
;
	GLOBAL	PHREAD
	GLOBAL	PHWRITE
	GLOBAL	HOME
	GLOBAL	FHINIT
	GLOBAL	SELDSK
	GLOBAL	RQPHUN

;
;
; EXTERNALS
;
;
	EXTRN	FLREAD
	EXTRN	FLWRITE
	EXTRN	FLHOME
	EXTRN	FLINIT
	EXTRN	FINTVC
	EXTRN	HDREAD
	EXTRN	HDWRITE
	EXTRN	HDHOME
	EXTRN	HDINIT
;
	EXTRN	FDCSEC
	EXTRN	FDCLEN
	EXTRN	FDCADR
	EXTRN	FDCBAS
	EXTRN	FDCTRK
;
	EXTRN	HSTWRT
	EXTRN	HSTACT
	EXTRN	SEKDSK
	EXTRN	RQUNIT	;REQUESTED UNIT
	EXTRN	PHUNIT	;PHYSICAL UNIT

	EXTRN	BASETB
	EXTRN	DENB
	EXTRN	FDCDEN
	EXTRN	FTRKUN
	EXTRN	PTRKUN
	EXTRN	HDSECT
	EXTRN	HDRVTB	; HARD DISK PHYSICAL DRIVE UNIT TABLE
	EXTRN	HDRVUN	; PHYSICAL HARDDISK DRIVEUNIT NUMBER
	EXTRN	PTRINI
	EXTRN	HDSKDF	; HARD DISK DEFINITION
;
	EXTRN	BREAK
	EXTRN	COLDFL
;
;
; CONSTANTEN
;
DOUBLE	EQU	0
SINGLE	EQU	1
VIRTDSK	EQU	'M'-'A'	; MEMORY DISK
;
;
;
;PROCEDURES
;
PHREAD:
	CALL	RQPHUN
	JP	NC,FLREAD

	JP	HDREAD
;
 
PHWRITE:
	CALL	RQPHUN
	JP	NC,FLWRITE
	JP	HDWRITE
;

 
HOME:
	CALL	RQPHUN
	JP	NC,FLHOME

	LD	A,(HSTWRT)
	OR	A
	JR	NZ,HOME1
	LD	(HSTACT),A
HOME1:
	LD	A,0
	RET
;
;
; UNIT CONVERSION
;
RQPHUN:
	LD	A,(RQUNIT)	; REQUESTED UNIT
	CP	4		; 0-3 => FLOPPY
	JP	NC,RPUNHD	; HARD DISK REQUEST
RPUNFL:				; FLOPPY REQUEST
	LD	B,A		; SAVE RQUNIT
	AND	1		;0-1=PHYSICAL UNIT
	LD	(PHUNIT),A
	LD	A,B
	AND	2		; C:=0
	LD	(FDCDEN),A
	RET
RPUNHD:				; HARD DISK REQUESTED
	CP	VIRTDSK		; MEMORY DISK
	JP	Z,RPUNEX
	XOR	A
	LD	(PHUNIT),A
	CALL	HDSET
RPUNEX:
	SCF
	RET
;

;
HDSET:
	LD	A,(FDCSEC)
	DEC	A
	SLA	A
	SLA	A
	INC	A
	LD	(HDSECT),A
;
; DETERMINE PHYSICAL DRIVE UNIT NUMBER
;
	LD	A,(RQUNIT)
	SUB	4	; 4=>7 := 0=>3
	LD	E,A
	LD	D,0	; DE = UNIT
	LD	HL,HDRVTB	; TABLE ADDRESS
	ADD	HL,DE		; MODIFIED
	LD	A,(HL)		; PHYSICAL DRIVE
	LD	(HDRVUN),A
;
	RET
 
FHINIT:
	LD	HL,FINTVC
	LD	A,H
	LD	I,A
	IM	2
	LD	A,0
	LD	(FDCDEN),A	;DENSITY := DOUBLE
	LD	(RQUNIT),A
	LD	(PHUNIT),A
;
	LD	HL,100H
	LD	(FDCBAS),HL
;
	LD	HL,HDSKDF
	LD	(PTRINI),HL
;
	CALL	FLINIT
	CALL	MDINIT
	CALL	HDINIT
	RET	NC
	JP	BREAK
 
;
; INITIEER MEMORY DISK
;   BY FILLING DIRECTORY WITH "E5"
;
MDINIT:
;
; CHECK,COLD BOOT ?
;
	LD	A,(COLDFL)
	AND	A
	RET	Z
;
	LD	BC,0D00H	;BASE=200H
	LD	A,0FFH		;BASE PART = 0
	OUT	(C),A		; SET BASE
	LD	HL,0
MDINI1:
	LD	A,0E5H		; EMPTY DIRECTORY CODE
	LD	(HL),A
	INC	HL
	LD	A,H
	CP	10H		; ONE 4K BLOCK ?
	JP	NZ,MDINI1
;
; RESTORE BASE REGISTER
;
	LD	BC,0E00H	; BASE = 100H
	LD	A,0FFH		; BASE PART = 0
	OUT	(C),A
 
	RET
;
;*
;*  SELECT DISK ENTRY POINT
;*
;
SELDSK:
	LD	HL,0
	LD	A,C	; UNIT NUMBER
	CP	16	; MAX NUMBER
	RET	NC	; ERROR
	LD	(SEKDSK),A	; REMEMBER DISK NUMBER
	LD	(RQUNIT),A	; FOR A HOME !!
	SLA	A	; UNIT * 2
	LD	E,A
	LD	D,0	; DE =UNIT * 2

	LD	HL,BASETB	; DPBASE TABEL
	ADD	HL,DE	; CORRECT POINTER IN HL
	LD	E,(HL)	; LOWER BYTE
	INC	HL
	LD	D,(HL)	; UPPER BYTE
	EX	DE,HL	; DPBASE IN HL
	RET

	END
